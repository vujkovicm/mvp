######################################
# Author: Marijana Vujkovic
# Date: 02/27/2018
#
# imp-assoc.score
######################################

# generate phenotype file for outcome of interest from datacube and .config
# >
rule impPheno:
	message:
		"creating rvtest sample files for imputed {wildcards.phe} GWAS in {wildcards.pop} population"
	input:
		cube = "pheno/{phe}_datacube.modified.csv",
		pca  = "pop/MVP001.{pop}.pca"
	output:
		"out/imp/{phe}/{phe}.{pop}.phe"
	shell:
		"""
		Rscript scripts/R/imp/impPheno.R '{input.cube}' '{input.pca}' '{output}'
		"""

# extract selected covariates from .config
# >
def impCovar(filename):
	with open(filename, 'r') as f:
		str = f.readline()
	words = str.split()
	del words[0:5]
	words.remove(config['impPHE'][0])
	return ','.join(words)

# run population-specific GWAS (in chunks) on phenotype of interest
# >
rule scoreRvtest:
	message:
		"running imputed {wildcards.phe} GWAS in rvtest in {wildcards.pop} population [chr {wildcards.chr}; i {wildcards.i}; j {wildcards.j}]"
	input:
		phe = "out/imp/{phe}/{phe}.{pop}.phe"
	output:
		"out/imp/{phe}/{pop}/rvtest/chr{chr}/{phe}.chr{chr}.{i}.{j}.SingleScore.assoc"
	run:
		covar = impCovar(input.phe)
		shell("""rvtest --noweb --inVcf /data/data1/mvp001/mvp_imputed/1kg.ref_v20170306/chr{wildcards.chr}/chr{wildcards.chr}.{wildcards.i}.{wildcards.j}.dose.vcf.gz --pheno {input.phe} --pheno-name {wildcards.phe}	--covar {input.phe} --covar-name {covar} --single score --impute drop --dosage DS --out out/imp/{wildcards.phe}/{wildcards.pop}/rvtest/chr{wildcards.chr}/{wildcards.phe}.chr{wildcards.chr}.{wildcards.i}.{wildcards.j}""")

# create list of GWAS output-chunks
# >
rule scoreMrgList:
	message:
		"creating list of rvtest-chunks from imputed {wildcards.phe} GWAS in {wildcards.pop} population"
	input:
		"out/imp/{phe}/{pop}/rvtest/chr1/{phe}.chr1.1.01.SingleScore.out"
	output:
		"out/imp/{phe}/{pop}/rvtest/rvtest_chunks.score.lst"
	shell:
		"""
		file out/imp/{wildcards.phe}/{wildcards.pop}/rvtest/*/*.out | sed 's/[:].$//' >> {output}
		"""

# merge GWAS output chunks
# >
rule scoreMrgRvtest:
	message:
		"merging rvtest chunks with MAF > {wildcards.maf} from imputed {wildcards.phe} GWAS in {wildcards.pop} population"
	input:
		"out/imp/{phe}/{pop}/rvtest/rvtest_chunks.score.lst"
	output:
		"summary/imp/{phe}/{phe}.{pop}.imp.maf{maf}.score.out"
	shell:
		"""
		awk -F' ' 'FNR > 1 {{if ($6 >= {wildcards.maf} && $6 <= (1-{wildcards.maf})) print "chr"$1":"$2,$1,$2,$3,$4,$5,$6,$11,$12,$13}}' out/imp/{wildcards.phe}/{wildcards.pop}/rvtest/*/*.SingleScore.assoc > {output}
                # inser space seperated header to this file
                sed -i '1 i\CHRCBP CHR POS NEA EA N_INFORMATIVE EAF EFFECT SE PVALUE' {output}
		"""

# generate population-specific  Manhattan and QQ plots
# >
rule scoreManhattan:
	message:
		"creating manhattan and qq-plot from imputed {wildcards.phe} GWAS in {wildcards.pop} population"
	input:
		"summary/imp/{phe}/{phe}.{pop}.imp.maf{maf}.score.out"
	output:
		m = "summary/imp/{phe}/{phe}.{pop}.imp.info{info}.maf{maf}.score.m.png",
		q = "summary/imp/{phe}/{phe}.{pop}.imp.info{info}.maf{maf}.score.q.png",
		o = "summary/imp/{phe}/{phe}.{pop}.imp.info{info}.maf{maf}.score.out"
	shell:
		"""
		Rscript scripts/R/imp/impManhattan.R '{input}' '{output.m}' '{output.q}' '{wildcards.info}' '{output.o}'
		"""

# MVP-specific META-analysis
# >
rule scoreMetalScript:
	message:
		"creating metal script to run imputed {wildcards.phe} meta-analysis"
	input:
		eur = "summary/imp/{phe}/{phe}.EUR.imp.info{info}.maf{maf}.score.out",
		amr = "summary/imp/{phe}/{phe}.AMR.imp.info{info}.maf{maf}.score.out",
		afr = "summary/imp/{phe}/{phe}.AFR.imp.info{info}.maf{maf}.score.out"
	output:
		"scripts/metal/imp/{phe}.META.imp.info{info}.maf{maf}.score.metal"
	params:
		"summary/imp/{phe}/{phe}.META.imp.info{info}.maf{maf}.score.meta_ .out"
	shell:
		"""
		Rscript scripts/R/imp/impMetalScript.R '{output}' '{input.eur}' '{input.amr}' '{input.afr}' '{params}'
		"""

# run MVP-specific metal script
# >
rule scoreMetalRun:
	message:
		"running metal for imputed {wildcards.phe} meta-analysis"
	input:
		"scripts/metal/imp/{phe}.META.imp.info{info}.maf{maf}.score.metal"
	output:
		"summary/imp/{phe}/{phe}.META.imp.info{info}.maf{maf}.score.meta_1.out"
	shell:
		"""
		metal '{input}'
		"""

# clean up meta-analysis to standardized format
# >
rule scoreMetalClean:
	message:
		"cleaning up metal output from imputed {wildcards.phe} GWAS analysis"
	input:
		"summary/imp/{phe}/{phe}.META.imp.info{info}.maf{maf}.score.meta_1.out"
	output:
		all = "summary/imp/{phe}/{phe}.META.imp.info{info}.maf{maf}.score.all",
		sig = "summary/imp/{phe}/{phe}.META.imp.info{info}.maf{maf}.score.sig.out",
		tab = "summary/imp/{phe}/{phe}.META.imp.info{info}.maf{maf}.score.sig.tab"
	shell:
		"""
		Rscript scripts/R/imp/impMetalClean.R '{input}' '{output.all}' '{output.sig}' '{output.tab}'
		"""

# clean up GWAS-output to standardized format
# >
rule scorePopClean:
	message:
		"Prepare GWAS output from imputed {wildcards.phe} for {wildcards.pop}-specific locusZoom"
	input:
		"summary/imp/{phe}/{phe}.{pop}.imp.info{info}.maf{maf}.score.out"
	output:
		all = "summary/imp/{phe}/{phe}.{pop}.imp.info{info}.maf{maf}.score.all",
		sig = "summary/imp/{phe}/{phe}.{pop}.imp.info{info}.maf{maf}.score.sig.out",
		tab = "summary/imp/{phe}/{phe}.{pop}.imp.info{info}.maf{maf}.score.sig.tab"
	shell:
		"""
		Rscript scripts/R/imp/impPopClean.R '{input}' '{output.all}' '{output.sig}' '{output.tab}'
		"""

# prepare GWAS and META for LocusZOom
# >
rule scoreLocusPrep:
	message:
		"preparing output from imputed {wildcards.phe} meta-analysis for LocusZoom regional plots"
	input:
		"summary/imp/{phe}/{phe}.{pop}.imp.info{info}.maf{maf}.score.sig.tab"
	output:
		"summary/imp/{phe}/{phe}.{pop}.imp.info{info}.maf{maf}.score.locus.ref"
	shell:
		"""
		Rscript scripts/R/imp/impLocusPrep.R '{input}' '{output}'
		"""

# run LocusZoom in META and populations-specific GWAS
# >
rule scoreLocusRun:
	message:
		"[python 2.7] running LocusZoom for significant SNPs in imputed {wildcards.phe} meta-analysis"
	input:
		ref = "summary/imp/{phe}/{phe}.{pop}.imp.info{info}.maf{maf}.score.locus.ref",
		lz  = "summary/imp/{phe}/{phe}.{pop}.imp.info{info}.maf{maf}.score.out"
	output:
		"summary/imp/{phe}/locus/{phe}.{pop}.score.locus.txt"
	shell:
		"""
		Rscript scripts/R/imp/impLocusRun.R '{wildcards.phe}' '{wildcards.pop}' '{wildcards.info}' '{wildcards.maf}' 'score'
		"""
